// Works in all game modes. It reads 3 z-coordinates of camera (not actual player position) periodically
// and calcs speed and acceleration.
// If speed falls into danger zone ( fatalv <-> 1.25 fatalv ) checking for decceleration is enabled ; once decceleration occurs, player is killed. 

// So any wild movement puts you in wounded state; there mild deccelarations kill. For even more realism secondary health
// could be added. Once it is depleted, player is killed.

// To prevent unnecessarr bugs use firstperson view and set thirdpersondistance 0. Note that fall speed ( as coded in source of game ) is 
// not a function of fall height but but of the time spent falling.


// Most of the "bugs" are explained away by noting use of camera position (not actual player position)
// and falling time.



// No more unrealistic falls; unforgiving jumppads. By tweaking fatalv player can become really fragile.
// Real caution needed when playing with this.


//echo "TERMINAL VELOCITY by Ac"

thirdpersondistance 0 // otherwise death when reviving if camera is up

timestp = 100; // in msecs
fatalacc = 1000;
fatalv = -170; //default -150
checkk = 0
sleepd = 1000;

velm = 0; accm = 0

messagsb = 1; messags = [ messagsb = 1; sleep $sleepd [messags]]; messags
absf = [ if (>f $arg1 0) [ result $arg1 ] [ result (-f 0 $arg1) ] ]


timestp2 = (*f $timestp $timestp)
ptt2 = (at (getcampos) 2); ptt1 = $ptt2; ptt3 = $ptt2;
velocitycalc  = [ 
    ptt1 = $ptt2; ptt2 = $ptt3; ptt3 = (at (getcampos) 2);
    arg0 = (*f (divf (+f (-f $ptt3  (*f 2 $ptt2)) $ptt1) $timestp2) 1000000 ); // acc
    arg1 = (*f (divf (-f $ptt3 $ptt1) (*f 2 $timestp)) 1000 ); // speed

if (>f (absf $arg0) (absf $accm)) [ accm = $arg0 ] [];
if (>f (absf $arg1) (absf $velm)) [ velm = $arg1 ] [];

    if  (&& (<f $arg1 $fatalv) (>f $arg1 (*f 1.25 $fatalv))) [
            checkk = 1; ] [];
    if (&& (>f $arg0 $fatalacc) (= $checkk 1)) [kill;  accm = 0; velm = 0;  checkk = 0 ];

    if (= $messagsb 1) [
        //if (= $checkk 1) [echo "Warning"] [];
        //echo "Vel" $velm " Acc " $accm;
         messagsb = 0 ; velm = 0; accm = 0; // reset max velocity every 2 secs
                        ][];    
    sleep ($timestp) [ velocitycalc ]
    ]

velocitycalc
